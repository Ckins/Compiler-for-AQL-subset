create view DemocratCandidate as
    extract regex /(Hillary )?Clinton|(Bernie )?Sanders|(Martin )?O'Malley|(Lincoln )?Chafee|(Jim )?Webb/
        on D.text as DemocratCandidate
    from Document D;

create view RepublicanCandidate as 
    extract regex /(Jeb )?Bush|(Marco )?Rubio|(Scott )?Walker|(Ben )?Carson|(Mike )?Huckabee|(Rand )?Paul|(Rick )?Perry|(Chris )?Christie|(Ted )?Cruz|(Donald )?Trump|(John )?Kasich/
        on D.text as RepublicanCandidate
    from Document D;

create view Number as
    extract regex /[0-9]+(\.[0-9])?/
        on D.text as number
    from Document D;

create view Percent as
    extract regex /%|percent(s)?/
        on D.text as percent
    from Document D;

create view Ratio as
    extract pattern <N.number> <P.percent>
        return group 0 as ratio
    from Number N, Percent P;

create view DemCandidateWithRatio as
    extract pattern (<D.DemocratCandidate>) <Token>{1,20} (<R.ratio>)
        return group 0 as DemCandidateWithRatio
           and group 1 as DemocratCandidate
           and group 2 as Ratio
    from DemocratCandidate D, Ratio R;
    
create view RepCandidateWithRatio as
    extract pattern (<D.RepublicanCandidate>) <Token>{1,20} (<R.ratio>)
        return group 0 as RepCandidateWithRatio
           and group 1 as RepublicanCandidate
           and group 2 as Ratio
    from RepublicanCandidate D, Ratio R;

output view RepublicanCandidate;
output view DemocratCandidate;
output view Number;
output view Percent;
output view Ratio;
output view DemCandidateWithRatio;
